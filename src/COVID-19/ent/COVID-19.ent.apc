{Application 'COVID_19' logic file generated by CSPro}
PROC GLOBAL
function numeric uploadParadata() 
	if(syncconnect(CSWeb, "http://192.168.1.4:8099/csweb/api")) then
		syncparadata(PUT);
		syncdisconnect();
		uploadParadata = 1;
	else 
		uploadParadata = 0;
		errmsg("Can not connect to the server now, try later");
	endif;
end;

function getgps() 
    gps(open);
    if gps(read, 60) then
		LATITUDE = gps(latitude);
		LONGITUDE = gps(longitude);
    else
		errmsg("gps not working");
    endif;
    gps(close);
end;

function mapClicked(map myMap)
    myMap.clearMarkers();
    LAT = mymap.getLastClickLatitude();
    LONG = mymap.getLastClickLongitude();
    myMap.addMarker(mymap.getLastClickLatitude(), mymap.getLastClickLongitude());
end;

function closeMap(map myMap)
    myMap.hide();
end;

function getMap()
    map gmap;
    gmap.setBaseMap(satellite);
    gmap.setOnClick(mapClicked(gmap));
    gmap.addTextButton("Close map", closeMap(gmap));
    gmap.show();
end;

alpha photoDir = "./pictures";

//function to take photo with android //device
function string takePhoto(string picture_name, string suffix)
   //check if directory for saving pictures exists
   
   if(!direxist(photoDir)) then
		dircreate(photoDir);
   endif;
   string picture_path = concat(strip(photoDir), "/", strip(picture_name), strip(suffix)); 
   if(fileexist(picture_path)) then
       filedelete(picture_path);
   endif;
   execsystem(concat("camera:", pathconcat(Application, picture_path)));
   takePhoto = picture_path;
end;


PROC COVID_19_FF

PROC COVID_19_ID
preproc
PROC LAT
preproc
getMap();
PROC NAME

preproc
getgps();

postproc
if($ = "") then
 if(getlanguage() = "EN") then 
	errmsg("Name can not be emtpy");
	elseif(getlanguage() = "RO") then
	errmsg("Numele nu poate fi necompletat");
		endif;
	reenter;
endif;

PROC SURNAME
postproc
PICTURE = takePhoto(edit("999",COVID_19_ID), ".jpg");
PROC GENDER
postproc

list string missingBirthdayOptions;

 if(getlanguage() = "EN") then 
 
 
missingBirthdayOptions.add("Known");//1

missingBirthdayOptions.add("Unknown");//2

numeric selection = missingBirthdayOptions.show("Birthday of the patient is:");

if(selection = 2) then 
	DATE_OF_BIRTH = missing;
	skip to PHONE_NUMBER;
endif;


elseif(getlanguage() = "RO") then


 
missingBirthdayOptions.add("Cunoscut");//1

missingBirthdayOptions.add("necunoscut");//2

numeric selection = missingBirthdayOptions.show("Ziua de naștere a pacientului este:");

if(selection = 2) then 
	DATE_OF_BIRTH = missing;
	skip to PHONE_NUMBER;
endif;

endif;

PROC EMAIL
postproc

// Basic validation for email
if($ <> "") then
	if !regexmatch(EMAIL, "^[^@\s]+@[^@\s]+\.[^@\s]+$") then
		if(getlanguage() = "EN") then 
			errmsg(1, EMAIL);//Email not correct, plese fix
		elseif(getlanguage() = "RO") then
			errmsg(2); //Email nu este corect
		endif;
		reenter;
	endif;
endif;

PROC PATIENT_HAD_SYMPTOMS
postproc

if($ in 0,9) then
	skip to SAMPLE_TYPE;
endif;

PROC SAMPLE_TYPE_OTHER
preproc

ask if SAMPLE_TYPE = 9

PROC NAME_OF_THE_HOSPITAL
preproc

ask if HOSPITALIZATION_REQUIRED = 1

PROC X_RAY_DATE
preproc

ask if PNEUMONIA_BY_CHEST_X_RAY = 1 //Yes

PROC SPECY_INFECTION
preproc

ask if OTHER_INFECTIONS = 1 //Yes

PROC EMO
postproc
//uploadParadata();

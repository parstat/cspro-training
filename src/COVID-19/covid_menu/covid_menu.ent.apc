{Application 'COVID_MENU' logic file generated by CSPro}
PROC GLOBAL
//Function to upload data to the CSWeb server
function numeric synchroniseData() 
	if(syncconnect(CSWEb, "http://192.168.1.6:8099/csweb/api")) then
		if(OPTIONS = 1) then
			syncdata(PUT, COVID_19_DICT);
		elseif(OPTIONS = 2) then
			syncdata(GET, COVID_19_DICT);
		endif;
		syncdisconnect();
		synchroniseData = 1;
	else 
		synchroniseData = 0;
	endif;
end;

function setOptionsValueset() 
	if(ROLE = 1) then //user is a supervisor
		setvalueset(OPTIONS, OPTIONS_SUPERVIZOR);
	else  //user is an interviwer 
		setvalueset(OPTIONS, OPTIONS_INTERVIWER);
	endif;
end;

function setUserStatus(numeric userStatus) 
	STATUS = userStatus;
	writecase(USERS_DICT);
end;

function numeric incrementFailedLogins()
	FAILED_LOGIN = FAILED_LOGIN + 1;
	writecase(USERS_DICT);
	incrementFailedLogins = FAILED_LOGIN;
end;

function resetFailedLogins()
	FAILED_LOGIN = 0;
	writecase(USERS_DICT);
end;

PROC COVID_MENU_FF

PROC ID
postproc
if(!loadcase(USERS_DICT, $)) then
	errmsg("User id not found");
	$ = notappl;
	reenter;
endif;

PROC PIN
postproc 
//user id exists, checking if the pin is ok
if(FAILED_LOGIN < 4 & STATUS = 1) then
	if(USER_PIN = $) then  // pin is ok
		resetFailedLogins();
		setOptionsValueset();
	else //wrong pin
		//increment failed logins and check how many attempts left
		if (incrementFailedLogins() < 4) then
			errmsg("Pin is not correct, %d attemtp(s) left", 3 - FAILED_LOGIN);
			reenter;
		else 
			//Lock the user 
			setUserStatus(2);
			errmsg("User has been locked. Please contact your supervisor!")
				select("Change User", ID, "Contact supervisor", continue);
			execsystem("call:+355695336982");
			stop;
		endif;
	endif;
else
	errmsg("The user is locked or deleted")
		select("Change User", ID, "Stop the program", continue);
	stop;
endif;

PROC OPTIONS
postproc
if($ = 1 | $ = 2) then
	if(synchroniseData()) then
		errmsg("All data synchronized");
	else 
		errmsg("Connection with the server lost, please try again later");
	endif;
	$ = notappl;
	reenter;
elseif($ = 3) then
	if(execpff("../ent/COVID-19.pff", stop) = notappl) then
		errmsg("Could not start the data entry applciation");
	endif;
endif;
